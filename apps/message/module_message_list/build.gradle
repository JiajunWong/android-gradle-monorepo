if (isModule.toBoolean()) {
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
}

apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'dagger.hilt.android.plugin'

android {
    compileSdkVersion build_versions.target_sdk
    buildToolsVersion build_versions.build_tools

    defaultConfig {
        if (isModule.toBoolean()) {
            applicationId "com.sopherwang.message.module_message_list"
        }

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += ["room.schemaLocation": "$projectDir/schemas".toString(),
                              AROUTER_MODULE_NAME: project.getName()]
            }
        }

        minSdkVersion build_versions.min_sdk
        targetSdkVersion build_versions.target_sdk
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    sourceSets {
        main {
            if (isModule.toBoolean()) {
                manifest.srcFile 'src/main/moduleManifest/AndroidManifest.xml'
            } else {
                manifest.srcFile 'src/main/AndroidManifest.xml'
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    kapt {
        generateStubs = true
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
            unitTests.returnDefaultValues = true
        }
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation project(":apps:message:library_common_network")

    implementation deps.kotlin.stdlib
    implementation deps.core_ktx
    implementation deps.navigation.fragment_ktx
    implementation deps.navigation.ui_ktx

    implementation deps.material
    implementation deps.app_compat
    implementation deps.constraint_layout

    kapt deps.arouter.compiler
    implementation deps.arouter.api

    implementation deps.retrofit.retrofit
    implementation deps.retrofit.converter_gson
    implementation deps.retrofit.retrofit2_rxjava2_adapter

    implementation deps.rxjava2
    implementation deps.rx_android

    kapt deps.glide.compiler
    implementation deps.glide.runtime

    kapt deps.dagger.compiler
    implementation deps.dagger.runtime

    kapt deps.hilt.compiler
    kapt deps.hilt.android_compiler
    implementation deps.hilt.runtime
    implementation deps.hilt.view_model

    testImplementation deps.arch_core.testing
    testImplementation deps.junit
    testImplementation deps.mockito.core
    testImplementation deps.robolectric

    androidTestImplementation deps.atsl.core
    androidTestImplementation deps.atsl.ext_junit
    androidTestImplementation deps.atsl.runner
    androidTestImplementation deps.atsl.rules

    androidTestImplementation deps.arch_core.testing
    androidTestImplementation deps.espresso.core
    androidTestImplementation deps.espresso.contrib

    androidTestImplementation deps.mockito.core
    androidTestImplementation deps.mockito.android
}